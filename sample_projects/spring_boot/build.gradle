buildscript {
  dependencies {
    classpath "com.github.psxpaul:gradle-execfork-plugin:$pluginVersion"
  }
}

plugins {
  id 'org.springframework.boot' version '2.1.1.RELEASE'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

apply plugin: 'gradle-execfork-plugin'

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-web'
  testCompile 'junit:junit'
  testCompile 'org.hamcrest:hamcrest-library'
  testCompile 'org.apache.httpcomponents:fluent-hc:4.5.2'
}

task startDaemon(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'classes') {
  classpath = sourceSets.main.runtimeClasspath
  main = 'com.github.psxpaul.example.Main'
  jvmArgs = [ '-Dserver.port=9201' ]
  standardOutput = "$buildDir/springboot.log"
  errorOutput = "$buildDir/springboot-error.log"
  waitForPort = 9201
  timeout = 90
  stopAfter = test
}

test.dependsOn startDaemon
