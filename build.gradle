plugins {
  id 'com.gradle.plugin-publish' version '0.9.7'
  id 'org.jetbrains.kotlin.jvm' version '1.1.51'
  id 'idea'
  id 'maven'
}

group = 'com.github.psxpaul'
version = new File('VERSION').text.trim()
buildDir = "build/gradle"

dependencies {
  compile gradleApi()
  compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.51'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
}

pluginBundle {
  website = 'http://github.com/psxpaul'
  vcsUrl = 'https://github.com/psxpaul/gradle-execfork-plugin'
  description = 'Execute Java or shell processes in the background during a build'
  tags = ['java', 'exec', 'background', 'process']

  plugins {
    greetingsPlugin {
      id = 'com.github.psxpaul.execfork'
      displayName = 'Gradle Exec Fork Plugin'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

task cleanSampleProjects(type: GradleBuild, dependsOn: install) {
  buildFile = "${project.rootDir}/sample_projects/build.gradle"
  tasks = [ 'clean' ]
}
clean.finalizedBy cleanSampleProjects

task buildSampleProjects(type: GradleBuild, dependsOn: install) {
  buildFile = "${project.rootDir}/sample_projects/build.gradle"
  tasks = [ 'build' ]
}
build.finalizedBy buildSampleProjects

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
}

artifacts {
  archives javadocJar, sourcesJar
}
